if(rootProject.ext.BuildSdkPlayfun){
    apply plugin: 'com.android.library'
    apply plugin: 'com.kezong.fat-aar'
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-android-extensions'
}else{
    apply plugin: 'com.android.application'
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'com.google.firebase.crashlytics'
    apply from: 'aab_res_guard.gradle'
}
android {
    packagingOptions {
        if(rootProject.ext.BuildSdkPlayfun){
            exclude 'META-INF/DEPENDENCIES'
            exclude 'META-INF/NOTICE'
            exclude 'META-INF/NOTICE.txt'
            exclude 'META-INF/LICENSE'
            exclude 'META-INF/LICENSE.txt'
            exclude 'lib/x86/*.so'
            exclude 'lib/x86_64/*.so'
            exclude 'lib/armeabi/*.so'
            exclude 'lib/arm64-v8a/*.so'
        }else{
            exclude('META-INF/LICENSE')
            exclude('META-INF/beans.xml')
        }

    }

    signingConfigs {
        playChatRelease{
            storeFile file('../../keystore/PlayChat.jks')
            storePassword 'playchat'
            keyAlias 'playchat'
            keyPassword 'playchat'
            v1SigningEnabled true
            v2SigningEnabled true
        }
        playChatDebug{
            storeFile file('../../keystore/PlayChat.jks')
            storePassword 'playchat'
            keyAlias 'playchat'
            keyPassword 'playchat'
            v1SigningEnabled true
            v2SigningEnabled true
        }
        playFunDebug{

        }
    }
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        if(!rootProject.ext.BuildSdkPlayfun){
            applicationId "com.ld.play.chat"
        }
        // 版本号 - 注意，这里的版本号无效，请到productFlavors中设置
        versionCode 1
        // 版本名 - 注意，这里的版本名无效，请到productFlavors中设置
        versionName "1"
        minSdkVersion 21
        targetSdkVersion 30

        multiDexEnabled true

        // Maps SDK for Android 地图服务API_KEY https://developers.google.com/maps/documentation/android-sdk/get-api-key
        manifestPlaceholders = [mapsApiKey: "AIzaSyB0KW8QXuw7RNRAEeF2sUaFSWoqO0kUf_o"]

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        if(!rootProject.ext.BuildSdkPlayfun){
            if(rootProject.ext.BuildPlayChatRelease){
                signingConfig signingConfigs.playChatRelease
            }else{
                signingConfig signingConfigs.playChatDebug
            }
        }

        // vectorDrawables.useSupportLibrary = true
        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath true
            }
        }

        ndk {
            //设置支持的SO库架构
            abiFilters "armeabi-v7a","arm64-v8a"
        }
        //buildConfigField("boolean","isFinal","false")
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if(!rootProject.ext.BuildSdkPlayfun){
                if(!rootProject.ext.BuildPlayChatRelease){
                    signingConfig signingConfigs.playChatDebug
                }
            }
        }
        release {
            zipAlignEnabled = true //是否开启zip压缩
            minifyEnabled true   //是否代码混淆
            multiDexEnabled true //防止方法数量超过65536导致错误
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.playChatRelease
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    dataBinding { enabled = true }

    flavorDimensions "distribute"
    //定义的配置 最终会在Build Variants里面呈现出来
    productFlavors {
        // PlayChat台湾版配置 BuildPlayChatRelease (false = Debug 或 true = 正式包)
        PlayChat {
            dimension "distribute"
            applicationId "com.ld.play.chat"
            manifestPlaceholders = [package_name: applicationId]
            if(!BuildPlayChatRelease){
                //测试包配置
                signingConfig signingConfigs.playFunDebug
                // 腾讯IM App KEY
                buildConfigField "int", "IM_APP_KEY", "1400625290"//"1400510510"
                //buildConfigField "int", "IM_APP_KEY", "1400369342"
                // 腾讯IM FCM离线推送ID
                //buildConfigField "int", "GOOGLE_FCM_PUSH_BUZID", "13127"17321
                buildConfigField "int", "GOOGLE_FCM_PUSH_BUZID", "18414"
                // 服务端接口HOST
                buildConfigField "String", "API_BASE_URL", "\"https://t-api.joy-mask.com/\""
                // 图片HOST
                buildConfigField "String", "IMAGE_BASE_URL", "\"https://img.joy-mask.com/\""
                //H5网页地址
                buildConfigField "String", "WEB_BASE_URL", "\"http://t-m.joy-mask.com/\""
                // Ali OSS BucketName
                buildConfigField "String", "BUCKET_NAME", "\"joymask\""
                //app名字
                resValue "string","app_name","PlayChat-beta"
                //Facebook APPID
                resValue "string","facebook_app_id","5173832349346696"
                //Flyerid
                resValue "string","app_flyer","J4YbG6jNVHaFWuAeixmCuC"
                //facebook 登录key
                resValue "string","fb_login_protocol_scheme","fb5173832349346696"
                //Google公钥
                resValue "string","google_public_key","MIIBIjANBgkqhkiG9w0BAQEFAAOCAQ8AMIIBCgKCAQEAozfhLY9JLc+toLyhl/ZM/GbIEdNmqnrcLogdWs0rAOj+d8SXGBXWgtKBRgXGVKXxdf3jsmMJPO40Eow+AokHiNTBm/l8HBEadHkdcFPhnKzdvDs0ZsUpWdUYAJ+f7gcWwqfhuRSGUHkbGHrdDCXCJMBcfVGHaJM4lZ9wwH47Otvm88/F8DSKrXSZD/Xazqun0A/NyTsKP61jeGpUX67Ay0GolFewgt5pax5Qxh97O+YXDdxxO9iNSkAjnrAheF/JTm1+0mP/MdQ3u+hvOL7QacOZcnmKhJN3E+E0ZHRTXJi5eygDof1vqjiDJVhW3SnDmpktCYtd/1ANU0iUsaOTKwIDAQAB"
                // 版本号
                versionCode 5
                // 版本名
                versionName "1.0.1-beta"
            }else{
                //正式包配置
                signingConfig signingConfigs.playChatRelease
                // 腾讯IM App KEY 1400504347
                buildConfigField "int", "IM_APP_KEY", "1400413943"
                //buildConfigField "int", "IM_APP_KEY", "1400504347"
                // 腾讯IM FCM离线推送ID
                buildConfigField "int", "GOOGLE_FCM_PUSH_BUZID", "17321"
                // Bugly App Id
                buildConfigField "String", "BUGLY_APP_ID", "\"f208faedec\""
                // 服务端接口HOST
                buildConfigField "String", "API_BASE_URL", "\"https://api.play-chat.net/\""
                // 图片HOST
                buildConfigField "String", "IMAGE_BASE_URL", "\"https://img.play-chat.net/\""
                //H5网页地址
                buildConfigField "String", "WEB_BASE_URL", "\"https://m.play-chat.net/\""
                // Ali OSS BucketName
                buildConfigField "String", "BUCKET_NAME", "\"joymask\""
                //app名字
                resValue "string","app_name","聊玩"
                //Facebook APPID
                resValue "string","facebook_app_id","5173832349346696"
                //Flyerid
                resValue "string","app_flyer","J4YbG6jNVHaFWuAeixmCuC"
                //facebook 登录key
                resValue "string","fb_login_protocol_scheme","fb5173832349346696"
                //Google公钥
                resValue "string","google_public_key","MIIBIjANBgkqhkiG9w0BAQEFAAOCAQ8AMIIBCgKCAQEAozfhLY9JLc+toLyhl/ZM/GbIEdNmqnrcLogdWs0rAOj+d8SXGBXWgtKBRgXGVKXxdf3jsmMJPO40Eow+AokHiNTBm/l8HBEadHkdcFPhnKzdvDs0ZsUpWdUYAJ+f7gcWwqfhuRSGUHkbGHrdDCXCJMBcfVGHaJM4lZ9wwH47Otvm88/F8DSKrXSZD/Xazqun0A/NyTsKP61jeGpUX67Ay0GolFewgt5pax5Qxh97O+YXDdxxO9iNSkAjnrAheF/JTm1+0mP/MdQ3u+hvOL7QacOZcnmKhJN3E+E0ZHRTXJi5eygDof1vqjiDJVhW3SnDmpktCYtd/1ANU0iUsaOTKwIDAQAB"
                // 版本号
                versionCode 8
                // 版本名
                versionName "1.0.4"
            }
        }
        // 台湾版配置-Test   playFunDebug
        playFunDebug {
            signingConfig signingConfigs.playFunDebug
            dimension "distribute"
            // 腾讯IM App KEY
            buildConfigField "int", "IM_APP_KEY", "1400625290"//"1400510510"
            //buildConfigField "int", "IM_APP_KEY", "1400369342"
            // 腾讯IM FCM离线推送ID
            //buildConfigField "int", "GOOGLE_FCM_PUSH_BUZID", "13127"17321
            buildConfigField "int", "GOOGLE_FCM_PUSH_BUZID", "18414"
            // 服务端接口HOST
            buildConfigField "String", "API_BASE_URL", "\"https://t-api.joy-mask.com/\""
            // 图片HOST
            buildConfigField "String", "IMAGE_BASE_URL", "\"https://img.joy-mask.com/\""
            //H5网页地址
            buildConfigField "String", "WEB_BASE_URL", "\"https://t-m.joy-mask.com/\""
            // Ali OSS BucketName
            buildConfigField "String", "BUCKET_NAME", "\"joymask\""
            //app名字
            resValue "string","app_name","PlayChat-beta"
            //Facebook APPID
            resValue "string","facebook_app_id","5173832349346696"
            //Flyerid
            resValue "string","app_flyer","J4YbG6jNVHaFWuAeixmCuC"
            //facebook 登录key
            resValue "string","fb_login_protocol_scheme","fb5173832349346696"
            //Google公钥
            resValue "string","google_public_key","MIIBIjANBgkqhkiG9w0BAQEFAAOCAQ8AMIIBCgKCAQEAozfhLY9JLc+toLyhl/ZM/GbIEdNmqnrcLogdWs0rAOj+d8SXGBXWgtKBRgXGVKXxdf3jsmMJPO40Eow+AokHiNTBm/l8HBEadHkdcFPhnKzdvDs0ZsUpWdUYAJ+f7gcWwqfhuRSGUHkbGHrdDCXCJMBcfVGHaJM4lZ9wwH47Otvm88/F8DSKrXSZD/Xazqun0A/NyTsKP61jeGpUX67Ay0GolFewgt5pax5Qxh97O+YXDdxxO9iNSkAjnrAheF/JTm1+0mP/MdQ3u+hvOL7QacOZcnmKhJN3E+E0ZHRTXJi5eygDof1vqjiDJVhW3SnDmpktCYtd/1ANU0iUsaOTKwIDAQAB"
            // 版本号
            versionCode 2
            // 版本名
            versionName "1.0.0-beta"
        }

    }
    //模块化设计方案 sourceSets配置
    sourceSets {
        //条件判断-是打apk包
        if(!rootProject.ext.BuildSdkPlayfun){
            if(!BuildPlayChatRelease){
                //java文件指定为 playChat模块下
                main.java.srcDirs=['src/main/java','src/playChat/java']
                //清单文件指定为 playChat模块下
                main.manifest.srcFile 'src/playChat/AndroidManifest.xml'
            }else{
                //替换java文件 合并替换debug、release下的相同代码文件
                main.java.srcDirs=['src/playChat/java', 'src/PlayChatRelease/java']
                main.manifest.srcFile 'src/PlayChatRelease/AndroidManifest.xml'
            }
            //其它资源文件
//            resources.srcDirs = ['src/playChat/res']
            main.res.srcDirs = ['src/playChat/res']
            main.assets.srcDirs = ['src/playChat/assets']
            main.jniLibs.srcDirs = ['src/playChat/jniLibs']
        }
    }
    //条件判断-是打apk包
    if(!rootProject.ext.BuildSdkPlayfun){
        android.applicationVariants.all {
            variant ->
                variant.outputs.all {
                    //在这里修改apk文件名
                    outputFileName = "PlayChat-${variant.name}-v${variant.versionName}-${releaseTime()}.apk"
                }
        }
    }
}

def releaseTime() {
    return new Date().format("yyyyMMddHHmm", TimeZone.getTimeZone("GMT+08:00"))

}

repositories {
    flatDir {
        dirs 'libs'
    }
}

//条件判断-不是打apk包
if(rootProject.ext.BuildSdkPlayfun){
    afterEvaluate {
        // for app test, copy the final aar to application libs
        android.libraryVariants.all { variant ->
            File outputFile = variant.outputs.first().outputFile
            if(outputFile.isFile()){
                outputFile.delete();
            }
            tasks.named("assemble${variant.name.capitalize()}").configure {
                doLast {
                    copy {
                        from outputFile
                        into "../app/libs"
                        rename outputFile.name, "playfun-lib-1.5.aar"
                    }
                }
            }
        }
    }

    fataar {
        transitive = true
    }
}


dependencies {
    if(rootProject.ext.BuildSdkPlayfun){
        implementation fileTree(dir: "libs", include: ["*.jar"])
    }else{
        implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    }

    //日志打印
    implementation('com.github.ihsanbal:LoggingInterceptor:3.0.0') {
        exclude group: 'org.json', module: 'json'
    }
    implementation rootProject.ext.support["design"]
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'android.arch.navigation:navigation-fragment:1.0.0'
    implementation 'android.arch.navigation:navigation-ui:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    if(rootProject.ext.BuildSdkPlayfun){
        embed project(path: ':tuicore', configuration: 'default')
        embed project(path: ':tuiconversation', configuration: 'default')
        embed project(path: ':tuicontact', configuration: 'default')
        embed project(path: ':tuichat', configuration: 'default')
        embed project(path: ':tuicalling', configuration: 'default')
        embed project(path: ':mvvmhabit', configuration: 'default')
        embed project(path: ':jsBridge', configuration: 'default')
        embed project(path: ':customDrawable', configuration: 'default')
        embed project(path: ':AlivcMedia', configuration: 'default')
        embed project(path: ':AliyunCrop:crop', configuration: 'default')
        embed project(path: ':AliyunSVideoBase', configuration: 'default')
        embed project(path: ':AliyunVideoCommon', configuration: 'default')
        embed (name:'picture_library-v2.6.0', ext:'aar')
        embed (name:'ucrop-v2.6.0', ext:'aar')
        embed (name:'refresh-footer-classics-2.0.3', ext:'aar')
        embed (name:'refresh-drawable-paint-2.0.3', ext:'aar')


    }
    if(!rootProject.ext.BuildSdkPlayfun){
        implementation project(':tuiconversation')
        implementation project(':tuicontact')
        implementation project(':tuichat')
        implementation project(':tuicalling')
        implementation project(':mvvmhabit')
        implementation project(':jsBridge')
        implementation project(':customDrawable')
        implementation project (':AliyunCrop:crop')
        implementation project (':AliyunSVideoBase')
        implementation project(':AliyunVideoCommon')
    }
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.media2:media2-widget:1.1.2'

    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'


    implementation 'com.google.firebase:firebase-core:18.0.0'
    implementation "com.google.android.gms:play-services-location:17.1.0"
    implementation 'com.google.android.gms:play-services-maps:17.0.0'
    implementation 'com.google.firebase:firebase-analytics:18.0.0'
    implementation 'com.google.firebase:firebase-messaging:21.0.0'
    implementation 'com.google.firebase:firebase-iid:21.0.0'
    implementation 'com.android.billingclient:billing:4.0.0'
    implementation 'com.google.guava:guava:29.0-android'

    implementation platform('com.google.firebase:firebase-bom:28.0.1')
    implementation 'com.google.firebase:firebase-auth'
    implementation 'com.google.firebase:firebase-crashlytics'
    //Google谷歌登录
    implementation 'com.google.android.gms:play-services-auth:19.0.0'
    implementation 'com.google.firebase:firebase-analytics'

    implementation 'com.gyf.immersionbar:immersionbar:3.0.0'
    implementation 'com.blankj:utilcode:1.28.4'
    implementation 'com.kaopiz:kprogresshud:1.2.0'

    // 直接引用源码，取消AAR的引用
    implementation 'com.contrarywind:Android-PickerView:4.1.9'
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.50'
    implementation 'com.lxj:xpopup:1.9.0'
    implementation 'org.glassfish:javax.annotation:10.0-b28'

    implementation 'com.scwang.smart:refresh-layout-kernel:2.0.3'
    implementation 'com.scwang.smart:refresh-header-material:2.0.3'
//   PopupWindow 的封装
    implementation 'com.github.zyyoona7:EasyPopup:1.1.2'

    implementation 'com.tencent:mmkv-static:1.1.2'

    // To get SwipeBack feature, rely on both fragmentation & fragmentation-swipeback
    implementation 'me.yokeyword:fragmentationx:1.0.2'

    implementation 'com.github.chrisbanes:PhotoView:2.3.0'

    implementation 'com.aliyun.dpa:oss-android-sdk:2.9.4'

    implementation 'jp.wasabeef:blurry:3.0.0'

    implementation 'top.zibin:Luban:1.1.8'
    implementation 'com.devbrackets.android:exomedia:4.3.0'
    implementation 'com.youth.banner:banner:1.4.10'  //最新版本

    implementation 'com.android.installreferrer:installreferrer:2.2'

    //今日头条适配
    implementation 'com.github.JessYanCoding:AndroidAutoSize:v1.2.1'
    //高斯模糊图片类
    implementation 'jp.wasabeef:glide-transformations:4.3.0'
    // 动画解析库：https://github.com/airbnb/lottie-android
    // 动画资源：https://lottiefiles.com、https://icons8.com/animated-icons
    implementation 'com.airbnb.android:lottie:3.6.1'

    //视频列表播放插件 完整版引入
    implementation 'com.github.CarGuo.GSYVideoPlayer:GSYVideoPlayer:v8.1.7-release-jitpack'
    //SVGA动画播放库
    implementation 'com.github.svga:SVGAPlayer-Android:2.6.1'

    implementation "androidx.viewpager2:viewpager2:1.0.0"
}
